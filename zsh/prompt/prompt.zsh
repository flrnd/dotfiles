setopt prompt_subst
#autoload -U colors && colors # Enable colors in prompt

# Echoes a username/host string when connected over SSH (empty otherwise)
ssh_info() {
	[[ "$SSH_CONNECTION" != '' ]] && echo '%F{green}%n@%m%f%F{white}: %f' || echo ''
}

# Echoes information about Git repository status when inside a Git repository
git_info() {

  # Exit if not inside a Git repository
  ! git rev-parse --is-inside-work-tree > /dev/null 2>&1 && return

  # Git branch/tag, or name-rev if on detached head
  local GIT_LOCATION=${$(git symbolic-ref -q HEAD || git name-rev --name-only --no-undefined --always HEAD)#(refs/heads/|tags/)}

  local AHEAD="%F{cyan}>NUM%f%F{white}%f"
  local BEHIND="%F{cyan}<NUM%f%F{white}%f"
  local MERGING="%F{magenta}M%f%F{white}%f"
  local UNTRACKED="%F{yellow}?%f%F{white}%f"
  local MODIFIED="%F{red}*%f%F{white}%f"
  local STAGED="%F{green}+%f%F{white}%f"
  local STASHED="%F{magenta}#%f%F{white}%f"

  local -a DIVERGENCES
  local -a FLAGS

  local NUM_AHEAD="$(git log --oneline @{u}.. 2> /dev/null | wc -l | tr -d ' ')"
  if [ "$NUM_AHEAD" -gt 0 ]; then
    DIVERGENCES+=( "${AHEAD//NUM/$NUM_AHEAD}" )
  fi

  local NUM_BEHIND="$(git log --oneline ..@{u} 2> /dev/null | wc -l | tr -d ' ')"
  if [ "$NUM_BEHIND" -gt 0 ]; then
    DIVERGENCES+=( "${BEHIND//NUM/$NUM_BEHIND}" )
  fi

  local GIT_DIR="$(git rev-parse --git-dir 2> /dev/null)"
  if [ -n $GIT_DIR ] && test -r $GIT_DIR/MERGE_HEAD; then
    FLAGS+=( "$MERGING" )
  fi

  if [[ -n $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
    FLAGS+=( "$UNTRACKED" )
  fi

  if ! git diff --quiet 2> /dev/null; then
    FLAGS+=( "$MODIFIED" )
  fi

  if ! git diff --cached --quiet 2> /dev/null; then
    FLAGS+=( "$STAGED" )
  fi

  if [[ -n $(git stash list 2> /dev/null) ]]; then
    FLAGS+=( "$STASHED" )
  fi

  local -a GIT_INFO
  GIT_INFO+=( "%F{green}$GIT_LOCATION%f%F{white}%f" )
  [ -n "$GIT_STATUS" ] && GIT_INFO+=( "$GIT_STATUS" )
  [[ ${#DIVERGENCES[@]} -ne 0 ]] && GIT_INFO+=( "${(j::)DIVERGENCES}" )
  [[ ${#FLAGS[@]} -ne 0 ]] && GIT_INFO+=( "${(j::)FLAGS}" )
  echo "${(j: :)GIT_INFO}"

}

# Use ‚ùØ as the non-root prompt character; # for root
# Change the prompt character color if the last command had a nonzero exit code
#RPROMPT='%*'
PROMPT='$(ssh_info)%F{blue}%~%u%f $(git_info)
%(?.%F{magenta}.%F{red})%(!.#.$)%f%F{white}%f '
